\section{Formalizing X25519 from RFC~7748}
\label{sec:Coq-RFC}

In this section we present our formalization of RFC~7748~\cite{rfc7748}.

\begin{informaltheorem}
  The specification of X25519 in RFC~7748 is formalized by the function \Coqe{RFC} in Coq.
\end{informaltheorem}

More specifically, we formalized X25519 with the following definition:
\begin{lstlisting}[language=Coq]
Definition RFC (n: list Z) (p: list Z) : list Z :=
  let k := decodeScalar25519 n in
  let u := decodeUCoordinate p in
  let t := montgomery_rec_swap
    255  (* iterate 255 times  *)
    k    (* clamped n          *)
    1    (* x_2                 *)
    u    (* x_3                 *)
    0    (* z_2                 *)
    1    (* z_3                 *)
    0    (* dummy              *)
    0    (* dummy              *)
    u    (* x_1                 *)
    0    (* previous bit = 0   *) in
  let a := get_a t in
  let c := get_c t in
  let o := ZPack25519 (Z.mul a (ZInv25519 c))
  in encodeUCoordinate o.
\end{lstlisting}

In this definition \coqe{montgomery_rec_swap} is a generic ladder instantiated
with integers and defined as follows:
\begin{lstlisting}[language=Coq]
Fixpoint montgomery_rec_swap (m : nat) (z : T')
(a: T) (b: T) (c: T) (d: T) (e: T) (f: T) (x: T) (swap:Z) :
(* a:    x2                 *)
(* b:    x3                 *)
(* c:    z2                 *)
(* d:    z3                 *)
(* e:    temporary  var     *)
(* f:    temporary  var     *)
(* x:    x1                 *)
(* swap: previous bit value *)
(T * T * T * T * T * T) :=
match m with
| S n =>
  let r := Getbit (Z.of_nat n) z in
    (* k_t = (k >> t) & 1                        *)
  let swap := Z.lxor swap r in
    (* swap ^= k_t                               *)
  let (a, b) := (Sel25519 swap a b, Sel25519 swap b a) in
    (* (x_2, x_3) = cswap(swap, x_2, x_3)            *)
  let (c, d) := (Sel25519 swap c d, Sel25519 swap d c) in
    (* (z_2, z_3) = cswap(swap, z_2, z_3)            *)
  let e := a + c in (* A  = x_2 + z_2                *)
  let a := a - c in (* B  = x_2 - z_2                *)
  let c := b + d in (* C  = x_3 + z_3                *)
  let b := b - d in (* D  = x_3 - z_3                *)
  let d := e^2    in (* AA = A^2                     *)
  let f := a^2    in (* BB = B^2                     *)
  let a := c * a in (* CB = C * B                  *)
  let c := b * e in (* DA = D * A                  *)
  let e := a + c in (* x_3= (DA + CB)^2              *)
  let a := a - c in (* z_3= x_1* (DA - CB)^2          *)
  let b := a^2    in (* z_3= x_1* (DA - CB)^2          *)
  let c := d - f in (* E  = AA - BB                *)
  let a := c * C_121665 in
                    (* z_2 = E * (AA + a24 * E)     *)
  let a := a + d in (* z_2 = E * (AA + a24 * E)     *)
  let c := c * a in (* z_2 = E * (AA + a24 * E)     *)
  let a := d * f in (* x_2 = AA * BB                *)
  let d := b * x in (* z_3 = x_1* (DA - CB)^2       *)
  let b := e^2    in (* x_3 = (DA + CB)^2           *)
  montgomery_rec_swap n z a b c d e f x r
    (* swap = k_t                              *)

| 0%nat =>
  let (a, b) := (Sel25519 swap a b, Sel25519 swap b a) in
    (* (x_2, x_3) = cswap(swap, x_2, x_3)          *)
  let (c, d) := (Sel25519 swap c d, Sel25519 swap d c) in
    (* (z_2, z_3) = cswap(swap, z_2, z_3)          *)
  (a,b,c,d,e,f)
end.
\end{lstlisting}

The comments in the ladder represent the text from the RFC which
our formalization matches perfectly. In order to optimize the
number of calls to \texttt{CSWAP} (defined in \sref{cswap})
the RFC uses an additional variable to decide whether a conditional swap
is required or not.

Later in our proof we use a simpler description of the ladder
(\coqe{montgomery_rec}) which follows strictly \aref{alg:montgomery-ladder}
and prove those ladder equivalent.

RFC 7748 describes the calculations done in X25519 as follows:
\emph{``To implement the X25519(k, u) [...] functions (where k is
  the scalar and u is the u-coordinate), first decode k and u and then
  perform the following procedure, which is taken from [curve25519] and
  based on formulas from [montgomery].  All calculations are performed
  in GF(p), i.e., they are performed modulo p.''}~\cite{rfc7748}

Operations used in the Montgomery ladder of \coqe{RFC} are performed on
integers (See Appendix~\ref{subsubsec:RFC-Coq}).
The reduction modulo $\p$ is deferred to the very end as part of the
\coqe{ZPack25519} operation.

We now turn our attention to the decoding and encoding of the byte arrays.
We define the little-endian projection to integers as follows.
\begin{dfn}
  Let \Coqe{ZofList} : $\Z \rightarrow \texttt{list}~\Z \rightarrow \Z$,
  a function given $n$ and a list $l$ returns its little endian decoding with radix $2^n$.
\end{dfn}
% \begin{lstlisting}[language=Coq,aboveskip=0pt,belowskip=1pt]
% Fixpoint ZofList {n:Z} (a:list Z) : Z :=
%   match a with
%   | [] => 0
%   | h :: q => h + 2^n * ZofList q
%   end.
% \end{lstlisting}
Similarly, we define the encoding from integers to bytes.
\begin{dfn}
  Let \Coqe{ListofZ32} : $\Z \rightarrow \Z \rightarrow \texttt{list}~\Z$, given
  $n$ and $a$ returns $a$'s little-endian encoding as a list with radix $2^n$.
  %XXX-Peter: Again I'm confused... why are there two \rightarrows?
  %XXX-Benoit it is the functional view of arguments and partial functions. It is called Currying.
\end{dfn}
% \begin{lstlisting}[language=Coq,aboveskip=1pt,belowskip=1pt]
% Fixpoint ListofZn_fp {n:Z} (a:Z) (f:nat) : list Z :=
% match f with
%   | 0%nat => []
%   | S fuel => (a mod 2^n) :: ListofZn_fp (a/2^n) fuel
% end.

% Definition ListofZ32 {n:Z} (a:Z) : list Z :=
%   ListofZn_fp n a 32.
% \end{lstlisting}
% In order to increase the trust in our formalization, we prove that
% \Coqe{ListofZ32} and \Coqe{ZofList} are inverse to each other.
% \begin{lstlisting}[language=Coq,aboveskip=1pt,belowskip=1pt]
% Lemma ListofZ32_ZofList_Zlength: forall (l:list Z),
%   Forall (fun x => 0 <= x < 2^n) l ->
%   Zlength l = 32 ->
%   ListofZ32 n (ZofList n l) = l.
% \end{lstlisting}

With those tools at hand, we formally define the decoding and encoding as
specified in the RFC.
\begin{lstlisting}[language=Coq]
Definition decodeScalar25519 (l: list Z) : Z :=
  ZofList 8 (clamp l).

Definition decodeUCoordinate (l: list Z) : Z :=
  ZofList 8 (upd_nth 31 l (Z.land (nth 31 l 0) 127)).

Definition encodeUCoordinate (x: Z) : list Z :=
  ListofZ32 8 x.
\end{lstlisting}

In the definition of \coqe{decodeScalar25519}, \coqe{clamp} is taking care of
setting and clearing the selected bits as stated in the RFC and described
in~\sref{subsec:X25519-key-exchange}.
